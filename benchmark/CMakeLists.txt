SET(${PROJECT_NAME}_BENCHMARK
  boxqp
  unicycle
  lqr
  arm-manipulation
  quadrupedal-gaits
  bipedal-timings
  )


IF(BUILD_WITH_CODEGEN_SUPPORT)
  SET(${PROJECT_NAME}_CODEGEN_BENCHMARK
    arm-manipulation-cg
    bipedal-with-contact-cg
    )
  LIST(APPEND ${PROJECT_NAME}_BENCHMARK ${${PROJECT_NAME}_CODEGEN_BENCHMARK})
ENDIF()

FOREACH(BENCHMARK_NAME ${${PROJECT_NAME}_BENCHMARK})
  ADD_EXECUTABLE(${BENCHMARK_NAME} ${BENCHMARK_NAME}.cpp)
  TARGET_LINK_LIBRARIES(${BENCHMARK_NAME} ${PROJECT_NAME} example-robot-data::example-robot-data)
  ADD_CUSTOM_TARGET("benchmarks-cpp-${BENCHMARK_NAME}"
    ${BENCHMARK_NAME} \${INPUT})
ENDFOREACH(BENCHMARK_NAME ${${PROJECT_NAME}_BENCHMARK})

IF(BUILD_WITH_CODEGEN_SUPPORT)
  FOREACH(BENCHMARK_NAME ${${PROJECT_NAME}_CODEGEN_BENCHMARK})
    TARGET_LINK_LIBRARIES(${BENCHMARK_NAME} ${CMAKE_DL_LIBS})
  ENDFOREACH(BENCHMARK_NAME ${${PROJECT_NAME}_CODEGEN_BENCHMARK})
ENDIF()


IF(BUILD_PYTHON_INTERFACE)
  SET(${PROJECT_NAME}_BENCHMARK_PYTHON
    unicycle
    lqr
    arm_manipulation
    bipedal_walk
    quadrupedal_gaits
    )
  
  FOREACH(BENCHMARK_NAME ${${PROJECT_NAME}_BENCHMARK_PYTHON})
    PYTHON_BUILD(. "${BENCHMARK_NAME}.py")
    ADD_CUSTOM_TARGET("benchmarks-py-${BENCHMARK_NAME}"
      ${CMAKE_COMMAND} -E env PYTHONPATH=$ENV{PYTHONPATH}:${PROJECT_BINARY_DIR}/bindings/python
      ${PYTHON_EXECUTABLE} -c "from ${BENCHMARK_NAME} import *" \${INPUT})
  ENDFOREACH(BENCHMARK_NAME ${${PROJECT_NAME}_BENCHMARK_PYTHON})
ENDIF(BUILD_PYTHON_INTERFACE)
